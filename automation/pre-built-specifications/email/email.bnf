<start> ::= <empty> | <email>
<empty> ::= ""
<email> ::= <local> "@" <domain>
<local> ::= <atextordot> | <atextordot><local>
<domain> ::= <label> | <label> "." <domain>

<atextordot> ::= <atext> | "."
<atext> ::= <letter> | <digit> | <printable>

<label> ::= <let-dig> | <let-dig><labelext>
<labelext> ::= <let-dig> | <ldh-str><let-dig>

<ldh-str> ::= <let-dig-hyp> | <let-dig-hyp><ldh-str>
<let-dig-hyp> ::= <let-dig> | "-"
<let-dig> ::= <letter> | <digit>

<letter> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | 
             "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" |
             "U" | "V" | "W" | "X" | "Y" | "Z" | 
             "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | 
             "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" |
             "u" | "v" | "w" | "x" | "y" | "z" 

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<printable> ::= "!" | "#" | "$" | "%" | "&" | "'" | "*" | "+" | "-" | 
                "/" | "=" | "?" | "^" | "_" | "`" | "{" | "|" | "}" | "~"